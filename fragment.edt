// Save current paragraph in current TeX Document.

  Requires(20110504);

  PushTagsandRegisters;

:Prepare:: ==========================================================
  IfisMode("TeX","%!m","","JMP('Exit')");
  IfisMode("STY","%!m","JMP('Exit')","");
  IfisMode("AUX","%!m","JMP('Exit')","");
  IfFileExists("%@('APPDATA');\QuadView\running","","JMP('Exit')");

  OpenOutput("%@('APPDATA');\QuadView\directory.txt",0,0,0,"%F");
  WrL("%P");
  CloseOutput;

:Fragment:: =========================================================

  LetReg(9, "%L");
  LetRegNum(4, %l);
  LetRegNum(5, %l);
  Loop(!|>
    LetRegNum(4, %!4-1);>
    IfNum(%!4, 0, '<=', 'Break', '');>
    GetLine(3, %!4);>
    IfStr('%!3', '', '=', 'Break', !'InsertByIndex(9, "%!9", "%!3%\", 0);');>
  |);
  Loop(!|>
    LetRegNum(5, %!5+1);>
    GetLine(3, %!5);>
    GetLength(6, "%!9");>
    IfStr('%!3', '', '=', 'Break', !'InsertByIndex(9, "%!9", "%\%!3", %!6);');>
  |);

:Compare:: ==========================================================

  Assign('quadview-fragment-old', '%$(|quadview-fragment|)');
  Assign('quadview-fragment', '%!9');
  IfStr('%$(|quadview-fragment|)', '%$(|quadview-fragment-old|)', '=', 'JMP("Exit")', '');
  IfFileExists("%@('APPDATA');\QuadView\minimal_preview.tex","","JMP('Preamble')");

:MinOutput:: ==========================================================
//Reusing Register 4
  ReadFile( "%@('APPDATA');\QuadView\minimal_preview.tex", 4, 0, 0, 0 );
  OpenOutput("%@('APPDATA');\QuadView\fragment.tex",0,0,0,"%F");
  WrL("%$('quadview-version')");
  WrL("%$('quadview-minpreview')");
  WrL("%!4");
  WrL("\begin{document}");
  WrL("\begin{preview}");
  WrL("%$('quadview-fragment')");
  WrL("\end{preview}");
  WrL("\end{document}");
  CloseOutput;
  JMP('Exit')

:Preamble:: =========================================================

  GetPreamble("\begin{document}",8);
  IfOK(!'Relax;',!|LetReg(8,>
    '\documentclass{article}%\'+>
    '\usepackage{amsmath}%\'>
  );|);

  FindInString("%!8\begin{document}%\", "%!9", -1, -1, 10);
  IfOK("JMP('Exit')");

:Output:: ===========================================================

  OpenOutput("%@('APPDATA');\QuadView\fragment.tex",0,0,0,"%F");
  WrL("%$('quadview-version')");
  WrL("%$('quadview-handout')");
  WrL("%!8");
  WrL("%$('quadview-preview')");
  WrL("\begin{document}");
  WrL;
  WrL("%$('quadview-fragment')");
  WrL;
  WrL("\end{document}");
  CloseOutput;

:Exit:: =============================================================

  PopTagsandRegisters;

End;
